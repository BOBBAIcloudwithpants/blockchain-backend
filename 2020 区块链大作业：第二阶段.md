# 2020 区块链大作业：第二阶段

## 个人信息

| 字段       | 值                               |
| ---------- | -------------------------------- |
| 学院与专业 | 数据科学与计算机学院软件工程专业 |
| 姓名       | 白家栋                           |
| 学号       | 18342001                         |



## 项目设计说明

根据需求，本次基于 **fisco-bcos** 设计的智能合约需要满足以下的四个需求:

- **功能一**：实现采购商品—签发应收账款 交易上链。例如车企从轮胎公司购买一批轮胎并签订应收账款单据。

- **功能二**：实现应收账款的转让上链，轮胎公司从轮毂公司购买一笔轮毂，便将于车企的应收账款单据部分转让给轮毂公司。轮毂公司可以利用这个新的单据去融资或者要求车企到期时归还钱款。

- **功能三**：利用应收账款向银行融资上链，供应链上所有可以利用应收账款单据向银行申请融资。

- **功能四**：应收账款支付结算上链，应收账款单据到期时核心企业向下游企业支付相应的欠款。 

从背景设定以及需求中，我设计了以下的这两个角色:

### 一. 角色设计

#### 角色1: Company

Company 在合约中的定义如下:

```c
    struct Company {
        // 公司名
        string name;
        // 公司地址，哈希值(在后端通过 get_account.sh 生成)
        address _addr;
        // 公司信用等级
        uint256 credit;
        // 公司资产
        uint256 asset;
    }
```

同时，Company 还具有 **核心企业**/**非核心企业** 的区分，保存在一个 `mapping` 中，如下：

```
// 由公司地址到公司级别的映射, 表明该公司是否是核心企业
    mapping(address => bool) is_upstream;
```

Company 能够完成如下的任务：

- **发起交易**：类比到供应链中即 **核心企业** 向下游企业采购货物等交易行为
- 创建**应收款账单**：类比到供应链中，即**核心企业**承诺给下游企业1000万来采购轮胎
- 与银行发起**融资**：类比到供应链中，即 **下游企业** 为了组装商品去找银行贷款，从而能够向其他公司购买原材料。 



#### 角色2: Bank

Bank 在合约中的定义如下:

```c++
    struct Bank {
        // 银行名
        string name;
        // 银行地址，哈希值
        address _addr;
    }
```

Bank 具有名称，地址的属性。银行默认资产是充足的，即有能力贷款给任何 Company. Bank 的主要职能是提供融资服务给所有企业，包括 **核心企业**, **下游企业** 等等。



### 二. 事件设计

除了角色之外，我还设计了3种发生在 Company 和 Bank 之间的事件，如下:



#### 事件1: Bill 

Bill 定义了企业之间的 **应收款账单**， Bill 的数据结构设计如下:

```c
// 一笔应收账款单
    struct Bill {
        // 账单id
        uint256 bill_id;
        // 申请借钱的公司
        address borrow_company;
        // 借出钱的公司
        address lend_company;
        // 应收金额
        uint256 money;
        // 借款日期
        uint256 start_date;
        // 还款日期
        uint256 end_date;
        // 是否通过审核
        bool is_pass;
        // 是否还款
        bool is_finish;
        // 是否是核心企业的账单
        bool is_from_upstream;
        // 收款公司的信用评级变动
        uint256 credit_growth;
    }
```

对于应收款账单的应用场景，类比到 **供应链中**， 当核心企业，比如宝马向轮胎公司订购一批轮胎时，宝马作为 **核心企业** 就和下游的轮胎公司共同签发了一份 **Bill** ，即应收账款账单。

同时，Bill 还具有后续的交易凭证的功能，比如轮胎公司能够通过其与宝马签订的 **Bill** 来从轮毂公司采购轮毂，这个过程通过将与宝马签订的 Bill 进行拆分完成的。拆分之后，当还款日期到达，宝马的还款方式从原有的按照最初的 **Bill** 上约定的金额付款给轮胎公司，变成了分别向轮胎公司和轮毂公司转账，这两笔转账之和与最初签订的 Bill 上的金额相等。

拆分 Bill 的函数实现如下:

```c
    // 转让一部分应收账单
    function splitBillToAnotherCompany(
        address from, // 从哪家公司转让
        address to, // 转让到哪家公司
        uint256 bid, // 被拆分的账单
        uint256 money, // 转让金额
        uint256 timestamp // 当前时间
    ) public returns (Message memory) {
        Bill memory bill_to_be_split;
        bool flag = false;
        for (uint256 i = 0; i < bills.length; i++) {
            if (bid == bills[i].bill_id) {
                flag = true;
                bill_to_be_split = bills[i];
                break;
            }
        }
        if (!flag) {
            return Message(false, "该账单不存在");
        }

        if (bill_to_be_split.lend_company != from) {
            return Message(false, "该账单并不属于该公司");
        }

        if (bill_to_be_split.borrow_company == to) {
            return Message(false, "该账单不可被转让，因为接收者需要还款");
        }

        if (money > bill_to_be_split.money) {
            return Message(false, "转让金额大于了账单原有金额");
        }

        if (timestamp > bill_to_be_split.end_date) {
            return Message(false, "该账单已经过期");
        }

        if (!bill_to_be_split.is_pass) {
            return Message(false, "该账单还没有通过审核");
        }

        if (bill_to_be_split.is_finish) {
            return Message(false, "该账单已完成还款，不可再被拆分");
        }
        // 鉴权完成，开始转让

        // 首先新增一份订单
        addBill(
            bill_to_be_split.borrow_company,
            to,
            money,
            timestamp,
            bill_to_be_split.end_date,
            false,
            false,
            bill_to_be_split.is_from_upstream,
            0
        );

        // 然后调整原有的账单金额
        adjustMoneyForBill(bid, bill_to_be_split.money - money);
        return Message(true, "转让完成, 等待审核中");
    }
```

当**核心企业** 与 **下游企业** 签订应还账款账单时，下游企业的信用值会根据借款金额增加，增加的规则如下:

```c
    // 计算转账后增加的信用等级
    function calculateCredit(
        address borrow_company, // 发起采购/借款的公司
        uint256 money, // 金额
        bool is_from_upstream // 是否来自于核心企业
    ) public view returns (uint256 growth) {
        if (!is_from_upstream) {
            return 0;
        }
        Company memory bc = getCompanyByAddress(borrow_company);
        uint256 prev_credit = bc.credit;
        return prev_credit / 10 + money / 50;
    }
```







#### 事件2: Deal

Deal 是整个供应链中的主体事件，车企向轮胎公司购买轮胎，轮胎公司向轮毂公司购买轮毂都是依靠 Deal 来完成的。

Deal 的数据结构定义如下:

```c
    // 交易
    struct Deal {
        // 交易id
        uint256 deal_id;
        // 出钱的一方, 买家
        address buyer_company;
        // 出货的一方，卖家
        address seller_company;
        // 交易发起开始日期
        uint256 start_date;
        // 交易是否完成
        bool is_finish;
        // 购买商品描述
        string merchant;
        // 购买金额
        uint256 money;
    }
```

基于 Deal，如果企业之间要发起交易，可以直接通过 `submitDeal` 函数发起。但是交易的能否通过，即`is_finish` 字段何时设置为 `true` 是有条件的，这个条件的判断方法为:

- 如果 `buyer_company` 为核心企业，则可以通过
- 如果 `buyer_company` 非核心企业，但是 `asset` 充足，也可以通过
- 如果 `buyer_company` 非核心企业，但是其持有 `money` 大于等于**Deal**所要求的购买金额的 **Bill**，也可以通过
- 若上述条件都不满足，则交易无法被通过。

上述的条件判断的实现如下:

```c
    function canBuyerSubmitDeal(address buyer_company, uint256 money)
        public
        view
        returns (bool can)
    {
        // 如果买家是核心企业，则可以交易
        if (is_upstream[buyer_company]) {
            return true;
        }

        // 如果买家的资金充足，也可以交易
        if (assets[buyer_company] >= money) {
            return true;
        }

        // 如果买家持有面值较大的账单，也可以交易
        if (canBuyerDealByBill(buyer_company, money)) {
            return true;
        }

        return false;
    }
```



#### 事件3: Loan

Loan 事件是企业向银行贷款时主要使用到的载体，Loan 的数据结构定义如下:

```c
    // 一笔向银行的融资
    struct Loan {
        // 融资账单id
        uint256 loan_id;
        // 申请融资的公司
        address request_company;
        // 银行地址
        address bank_address;
        // 发起日期
        uint256 start_date;
        // 还款日期
        uint256 end_date;
        // 是否通过审核
        bool is_pass;
        // 是否还款
        bool is_finish;
        // 融资金额
        uint256 money;
    }
```

发起融资请求后，银行会根据企业的**信用度**和**现有资产**进行贷款资格审核，具体审核规则如下:

- 如果企业的资产大于等于其贷款金额，则可以通过
- 如果企业的资产与其信用值乘50的和大于其贷款金额，则可以通过

实现如下:

```c
// 判断是否银行是否能为该公司提供贷款
    function canLoanToThisCompany(
        address company,
        address bank_address,
        uint256 current_credit,
        uint256 money
    ) public returns (bool can) {
        Company memory c = getCompanyByAddress(company);

        // 若贷款公司的资产大于其要借的钱数，则直接同意
        if (c.asset >= money) {
            return true;
        }

        // 若贷款公司的资产与其信用值乘50大于要借的钱数，也直接同意
        if (c.asset + current_credit * 50 >= money) {
            return true;
        }
        return false;
    }
```



## 功能测试文档

